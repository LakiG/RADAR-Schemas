//---------------------------------------------------------------------------//
// Configuration                                                             //
//---------------------------------------------------------------------------//

plugins {
    id 'com.commercehub.gradle.plugin.avro-base' version '0.9.1'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'com.commercehub.gradle.plugin.avro-base'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'

    // Configuration
    version = '0.6.1'
    group = 'org.radarbase'
    ext.githubRepoName = 'RADAR-base/RADAR-Schemas'

    ext.avroVersion = '1.9.2'

    ext.githubUrl = "https://github.com/${githubRepoName}.git"
    ext.issueUrl = "https://github.com/${githubRepoName}/issues"
    ext.website = 'http://radar-base.org'

    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8

    // dependencies

    dependencies {
        api (group: 'org.apache.avro', name: 'avro', version: avroVersion) {
            exclude group: 'org.xerial.snappy', module: 'snappy-java'
            exclude group: 'com.thoughtworks.paranamer', module: 'paranamer'
            exclude group: 'org.apache.commons', module: 'commons-compress'
            exclude group: 'org.tukaani', module: 'xz'
        }
    }

    // Generated avro files
    ext.avroOutputDir = file('src/generated/java')

    idea {
        module {
            downloadSources = true
        }
    }

    task downloadDependencies {
        description "Pre-downloads *most* dependencies"
        doLast {
            configurations.getAsMap().each { name, config ->
                println "Retrieving dependencies for $name"
                try {
                    config.files
                } catch (e) {
                    project.logger.info e.message // some cannot be resolved, silentlyish skip them
                }
            }
        }
    }
}

wrapper {
    gradleVersion '6.4.1'
}
